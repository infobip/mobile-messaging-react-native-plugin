def defaultCompileSDKVersion = 31
def defaultBuildToolsVersion = '31.0.0'
def defaultMinSDKVersion = 21
def defaultTargetSDKVersion = 31

def getRootProjectProperty(property, fallback) {
    rootProject.ext.has(property) ? rootProject.ext.get(property) : fallback
}

def overrideGmsVersion = getRootProjectProperty('overrideGmsVersion', '')
def overrideFirebaseVersion = getRootProjectProperty('overrideFirebaseVersion', '')
def overrideKotlinVersion = getRootProjectProperty('overrideKotlinVersion', '')
def withCryptorMigration = getRootProjectProperty('withCryptorMigration', false)

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

buildscript {
    // The Android Gradle plugin is only required when opening the android folder stand-alone.
    // This avoids unnecessary downloads and potential conflicts when the library is included as a
    // module dependency in an application project.
    // ref: https://docs.gradle.org/current/userguide/tutorial_using_tasks.html#sec:build_script_external_dependencies
    if (project == rootProject) {
        repositories {
            google()
            mavenLocal()
            mavenCentral {
                // We don't want to fetch react-native from Maven Central as there are
                // older versions over there.
                content {
                    excludeGroup "com.facebook.react"
                }
            }
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:4.2+'
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion getRootProjectProperty('compileSdkVersion', defaultCompileSDKVersion)
    buildToolsVersion getRootProjectProperty('buildToolsVersion', defaultBuildToolsVersion)
    defaultConfig {
        minSdkVersion getRootProjectProperty('minSdkVersion', defaultMinSDKVersion)
        targetSdkVersion getRootProjectProperty('targetSdkVersion', defaultTargetSDKVersion)
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    // ref: https://www.baeldung.com/maven-local-repository
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../node_modules/react-native/android"
    }
    maven {
        // Android JSC is installed from npm
        url "$rootDir/../node_modules/jsc-android/dist"
    }
    mavenCentral {
        // We don't want to fetch react-native from Maven Central as there are
        // older versions over there.
        content {
            excludeGroup "com.facebook.react"
        }
    }
    google()
    mavenLocal()
}

dependencies {
    def mmVersion = '6.2.1'

    implementation 'com.facebook.react:react-native:+'
    implementation "androidx.annotation:annotation:1.1.0"

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'

    implementation "com.infobip:infobip-mobile-messaging-android-resources:$mmVersion@aar"
    implementation "com.infobip:infobip-mobile-messaging-android-chat-sdk:$mmVersion@aar"
    implementation "com.infobip:infobip-mobile-messaging-android-geo-sdk:$mmVersion@aar"
    implementation ("com.infobip:infobip-mobile-messaging-android-sdk:$mmVersion@aar") {
        transitive = true;

        if (!overrideKotlinVersion.empty) {
            exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
        }

        if (!overrideGmsVersion.empty) {
            exclude group: 'com.google.android.gms', module: 'play-services-location'
        }
        if (!overrideFirebaseVersion.empty) {
            exclude group: 'com.google.firebase', module: 'firebase-messaging'
        }
    }
    if (!overrideGmsVersion.empty) {
        implementation "com.google.android.gms:play-services-location:$overrideGmsVersion"
    }
    if (!overrideFirebaseVersion.empty) {
        implementation "com.google.firebase:firebase-messaging:$overrideFirebaseVersion"
    }

    if (withCryptorMigration.toBoolean()) {
        implementation "com.infobip:infobip-mobile-messaging-android-cryptor-migration:$mmVersion@aar"
    }
}